WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident_syms = _{ "_" | "-" | "'" }
identifier = @{ ASCII_ALPHA+ ~ (ident_syms | ASCII_ALPHA | ASCII_DIGIT)* }

bitwidth = @{ ASCII_DIGIT+ }
num_lit = @{
      ASCII_DIGIT+ ~ "'"
      ~ ( "d" ~ ASCII_DIGIT+
        | "b" ~ ASCII_BIN_DIGIT+
        | "x" ~ ASCII_HEX_DIGIT+
        | "o" ~ ASCII_OCT_DIGIT+)
}

char = { !"\"" ~ ANY }
string_lit = ${ "\"" ~ char* ~ "\"" }

// ====== toplevel ======

component = {
      "component" ~ identifier ~ signature?
      ~ "{"
      ~ cells
      ~ connections
      ~ control
      ~ "}"
}

file = {
      SOI
      ~ imports
      ~ component+
      ~ EOI
}

import = _{
      "import" ~ string_lit ~ ";"
}

imports = { import* }


// ====== signature ======

signature = {
      "(" ~ io_ports? ~ ")" ~ signature_return?
}

signature_return = {
      "->" ~ "(" ~ io_ports? ~ ")"
}

io_port = {
      identifier ~ ":" ~ bitwidth
}

io_ports = {
      io_port ~ ("," ~ io_port)*
}

args = {
      "(" ~ (bitwidth ~ ("," ~ bitwidth)*)? ~ ")"
}

// ====== cells ======

primitive_cell = {
      identifier ~ "=" ~ "prim" ~ identifier ~ args
}

component_cell = {
      identifier ~ "=" ~ identifier
}

cells = {
      "cells"
      ~ "{" ~ ((primitive_cell | component_cell) ~ ";")* ~ "}"
}

// ====== wires ======

hole = {
      identifier ~ "[" ~ identifier ~ "]"
}

port = {
      (identifier ~ "." ~ identifier)
    | identifier
}

LHS = { hole | port }
expr = { LHS | num_lit }

guard_eq = { "==" }
guard_neq = { "!=" }
guard_leq = { "<=" }
guard_geq = { ">=" }
guard_lt = { "<" }
guard_gt = { ">" }
guard_or = { "|" }
guard_and = { "&" }
guard_not = { "!" }
operator = _{
      guard_eq | guard_neq | guard_leq | guard_geq
    | guard_lt | guard_gt | guard_or | guard_and
    | guard_not
}

guard_expr = {
      term ~ (operator ~ term)*
}

term = {
      guard_not ~ expr | guard_not ~ guard_expr | "(" ~ guard_expr ~ ")" | expr
}

switch_stmt = {
      guard_expr ~ "?" ~ expr
}

wire = {
      LHS ~ "=" ~ (switch_stmt | expr) ~ ";"
}

key_value = {
      string_lit ~ "=" ~ bitwidth
}

attributes = {
      "<" ~ (key_value ~ ("," ~ key_value)*) ~ ">"
}

group = {
      "group" ~ identifier ~ attributes? ~ "{"
      ~ wire*
      ~ "}"
}

connections = {
      "wires"
      ~ "{"
      ~ (wire | group)*
      ~ "}"
}

// ====== control ======

enable = { identifier ~ ";" }

seq = {
      "seq" ~ "{"
      ~ stmt*
      ~ "}"
}

par = {
      "par" ~ "{"
      ~ stmt*
      ~ "}"
}

block = _{
      "{" ~ stmt ~ "}"
}

if_stmt = {
      "if" ~ port ~ "with" ~ identifier ~ block ~ ("else" ~ (if_stmt | block))?
}

while_stmt = {
      "while" ~ port ~ "with" ~ identifier ~ block
}

stmt = {
      enable
    | (seq ~ ";"?)
    | (par ~ ";"?)
    | (if_stmt ~ ";"?)
    | (while_stmt ~ ";"?)
}

control = {
      "control"
      ~ "{"
      ~ stmt?
      ~ "}"
}
