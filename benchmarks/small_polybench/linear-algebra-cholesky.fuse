// BEGIN macro definitions



 // "fxp_sqrt.h"

// END macro definitions

import "math.h" {
  def sqrt(x: ubit<32>): ubit<32>;
}

decl A_int: ubit<32>[8][8];

for (let i: ubit<4> = 0..8) {
  // j < i
  let j: ubit<4> = 0 ;
  while(j < i) {
    decor "#pragma HLS loop_tripcount min=0 max=7 avg=4"
    let k: ubit<4> = 0;
    while(k < j) {
      decor "#pragma HLS loop_tripcount min=0 max=6 avg=2"
      let A_i_j = A_int[i][j];
      ---
      let A_i_k = A_int[i][k];
      ---
      let A_j_k = A_int[j][k];
      ---
      A_int[i][j] := A_i_j - A_i_k * A_j_k;
      // Update loop counter
      k := k + 1;
    }
    ---
    let A_j_j = A_int[j][j];
    ---
    let A_i_j = A_int[i][j];
    ---
    A_int[i][j] := A_i_j / A_j_j;

    // Update loop counter
    j := j + 1;
  }

  ---
  // i==j case
  let k: ubit<4> = 0;
  while(k < i) {
    decor "#pragma HLS loop_tripcount min=0 max=7 avg=4"
    let A_i_k = A_int[i][k];
    ---
    let A_i_i = A_int[i][i];
    ---
    A_int[i][i] := A_i_i - A_i_k * A_i_k;

    // Update loop counter
    k := k + 1;
  }
  ---
  let A_i_i = A_int[i][i];
  ---
  A_int[i][i] := sqrt(A_i_i);
}
