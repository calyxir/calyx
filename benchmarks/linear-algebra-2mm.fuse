// Macro definitions start

define(NI, 8)
define(NJ, 8)
define(NK, 8)
define(NL, 8)
define(DATATYPE, float)

// Macro definition end
import "printer.cpp" {
  def extern print_vec_2d(v: DATATYPE[NI][NI]);
}

decl alpha: DATATYPE;
decl beta: DATATYPE;
decl tmp: DATATYPE[NI][NJ];
decl A: DATATYPE[NI][NK];
decl B: DATATYPE[NK][NJ];
decl C: DATATYPE[NJ][NL];
decl D: DATATYPE[NI][NL];


for (let i = 0..NI) {
  for (let j = 0..NJ) {
    tmp[i][j] := 0.0;
    ---
    for (let k = 0..NK) {
      let v = alpha * A[i][k] * B[k][j]
    } combine {
      tmp[i][j] += v;
    }
  }
}

---

for (let i = 0..NI) {
  for (let j = 0..NL) {
    let d_tmp = D[i][j];
    ---
    D[i][j] := beta * d_tmp;
    ---
    for (let k = 0..NJ) {
      let v = tmp[i][k] * C[k][j]
    } combine {
      D[i][j] += v;
    }
  }
}
---
print_vec_2d(tmp);
print_vec_2d(D);
