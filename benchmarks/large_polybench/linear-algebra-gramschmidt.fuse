// BEGIN macro definitions





 // "fxp_sqrt.h"

// END macro definitions

import "math.h" {
  def sqrt(x: ubit<32>): ubit<32>;
}

decl A_int: ubit<32>[60][80];
decl R_int: ubit<32>[80][80];
decl Q_int: ubit<32>[60][80];

let A: ubit<32>[60][80];
let R: ubit<32>[80][80];
let Q: ubit<32>[60][80];

view A_sh = A[_: bank 1][_: bank 1];
view Q_sh = Q[_: bank 1][_: bank 1];
view R_sh = R[_: bank 1][_: bank 1];

for (let init0: ubit<7> = 0..80) {
  for (let init1: ubit<7> = 0..80) {
    R_sh[init0][init1] := R_int[init0][init1];
  }
}

for (let i0: ubit<7> = 0..60) {
  for (let j0: ubit<7> = 0..80) {
    A_sh[i0][j0] := A_int[i0][j0];
    Q_sh[i0][j0] := Q_int[i0][j0];
  }
}

---

for (let k: ubit<7> = 0..80) {
  let nrm: ubit<32> = 0;

  for (let i: ubit<7> = 0..60) {
    let A_i_k = A[i][k];
  } combine {
    nrm += A_i_k * A_i_k;
  }

  R[k][k] := sqrt(nrm);
  ---
  for (let i: ubit<7> = 0..60) {
    Q[i][k] := A[i][k] / R[k][k];
  }
  ---
  let j: ubit<7> = k + 1;
  while (j < 80) {
    decor "#pragma HLS loop_tripcount min=0 max=11 avg=6"
    R[k][j] := 0;
    ---
    for (let i: ubit<7> = 0..60) {
      let v: ubit<32> = Q[i][k] * A[i][j];
    } combine {
      R[k][j] += v;
    }
    ---
    for (let i: ubit<7> = 0..60) {
      let A_i_j = A[i][j];
      ---
      A[i][j] := A_i_j - Q[i][k] * R[k][j];
    }
    // Update loop counter
    j := j + 1;
  }
}
---
for (let init0: ubit<7> = 0..80) {
  for (let init1: ubit<7> = 0..80) {
    R_int[init0][init1] := R_sh[init0][init1];
  }
}

for (let i0: ubit<7> = 0..60) {
  for (let j0: ubit<7> = 0..80) {
    A_int[i0][j0] := A_sh[i0][j0];
    Q_int[i0][j0] := Q_sh[i0][j0];
  }
}
