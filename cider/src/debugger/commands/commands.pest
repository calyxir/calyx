WHITESPACE = _{ " " | "\t" }
dot        = _{ "." }
ident_syms = _{ "_" | "-" | "'" }
num        = @{ ASCII_DIGIT+ }

identifier = @{ ("_" | ASCII_ALPHA)+ ~ (ident_syms | ASCII_ALPHA | ASCII_DIGIT)* }

group = { (identifier ~ "::" ~ identifier | identifier) }

name = { identifier ~ (dot ~ identifier)* }

state_name = { name ~ ("[" ~ num ~ ("," ~ num)* ~ "]")? }
arb_str = @{(!(NEWLINE | WHITESPACE) ~ ANY)+}

pc_un      = { ^"u" }
pc_s       = { ^"s" }
pc_ufx     = { ^"u." ~ num }
pc_sfx     = { ^"s." ~ num }
code_calyx = { ^"calyx" }
code_nodes = { ^"nodes" }

print_code =  {
    "\\" ~ (pc_ufx | pc_sfx | pc_s | pc_un)
}
pc_fail    = @{ "\\" ~ ASCII_ALPHANUMERIC* }

print = {
    (^"print " | ^"p ") ~ (print_code)? ~ name+
}

print_state = { ^"print-state " ~ (print_code)? ~ state_name+ }

print_var = { (^"print-var " | ^"print-variable") ~ (print_code)? ~ arb_str+ }

print_fail = {
    (^"print-state" | ^"print" | ^"p") ~ (print_code | pc_fail)?
}

before = { ^"before" }
after  = { ^"after" }

watch_position = { before | after }

watch = {
    (^"watch " | ^"w ") ~ (watch_position)? ~ group ~ (^"with")? ~ (print_state | print)
}

step_over = { ^"step-over" ~ (group | breakpoint_path) ~ num? }

step       = { (^"step" | ^"s") ~ num? }
cont       = {
    (^"continue" | ^"c")
}
help       = {
    (^"help" | ^"h")
}
display    = { ^"display" | ^"d" }
info_break = { (^"info" ~ ^"break") | ^"i" ~ ^"b" }
info_watch = { (^"info" ~ ^"watch") | ^"i" ~ ^"w" }

break_target = { breakpoint_path | group }
brk          = { (^"break " | ^"br ") ~ break_target+ }
brk_id       = { (break_target | num) }

delete = { (^"delete " | ^"del ") ~ brk_id+ }

delete_watch = { (^"delete-watch " | ^"del-watch " | ^"delw ") ~ brk_id+ }

enable  = { (^"enable " | ^"en ") ~ brk_id+ }
disable = { (^"disable " | ^"dis ") ~ brk_id+ }

enable_watch  = { (^"enable-watch " | ^"enw ") ~ brk_id+ }
disable_watch = { (^"disable-watch " | ^"disw ") ~ brk_id+ }

exit = { ^"exit" | ^"quit" }

comm_where = { (^"where" | "pc") ~ (code_calyx | code_nodes)? }

explain = { ^"explain" }

restart = { ^"restart" }

command = {
    SOI ~ (watch | comm_where | print_state | print_var | print | print_fail | delete_watch | delete | brk | enable_watch | disable_watch | enable | disable | step_over | step // commands without input
  | cont | help | info_break | info_watch | display | exit | explain | restart)? ~ EOI
}

// note descriptor path
root            = _{ "." }
separator       = _{ "-" }
body            =  { "b" }
branch          =  { "t" | "f" }
clause          =  { separator ~ (body | num | branch) }
name_path       =  { (ASCII_ALPHA | ASCII_DIGIT | "_")+ }
breakpoint_path =  { name_path ~ root ~ clause* }
path            =  { SOI ~ breakpoint_path ~ EOI }
