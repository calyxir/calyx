WHITESPACE = _{ " " | "\t" | NEWLINE }
dot = _{"."}
ident_syms = _{ "_" | "-" | "'" }
num = @{ASCII_DIGIT+}



identifier = @{ ("_" | ASCII_ALPHA)+ ~ (ident_syms | ASCII_ALPHA | ASCII_DIGIT)* }

group = {(identifier ~ "::" ~ identifier | identifier)}

name = { identifier ~ (dot ~ identifier)* }

pc_un = { ^"u" }
pc_s = { ^"s" }
pc_ufx = { ^"u." ~ num }
pc_sfx = { ^"s." ~ num }
code_calyx = {^"calyx"}

print_code = { "\\" ~
    (
        pc_ufx |
        pc_sfx |
        pc_s |
        pc_un
    )
}
pc_fail = @{ "\\" ~ ASCII_ALPHANUMERIC* }

print = {
    (^"print" | ^"p") ~ (print_code)? ~ name+
}

print_state = {^"print-state" ~ (print_code)? ~ name+ }

print_fail = {
    (^"print-state" | ^"print" | ^"p") ~ (print_code | pc_fail)?
}

before = {^"before"}
after = {^"after"}

watch_position = { before | after }

watch = {
    (^"watch" | ^"w") ~ (watch_position)? ~ group ~ ^"with" ~ (print_state | print)
}

step_over = {^"step-over" ~ group }

step = { (^"step" | ^"s")  ~ num? }
cont = {
    (^"continue" | ^"c")
}
help = {
    (^"help" | ^"h")
}
display = { ^"display" | ^"d" }
info_break = { (^"info" ~ ^"break") | ^"i" ~ ^"b" }
info_watch = { (^"info" ~ ^"watch") | ^"i" ~ ^"w" }

brk = { (^"break" | ^"br") ~ group* }
brk_id = { (group | num) }

delete = { (^"delete" | ^"del") ~ brk_id* }

delete_watch = { (^"delete-watch" | ^"del-watch") ~ brk_id* }

enable = { (^"enable") ~ brk_id* }
disable = { (^"disable") ~ brk_id* }

exit = { ^"exit" | ^"quit" }


comm_where = { (^"where"| "pc") ~ (code_calyx)?}

explain = { ^"explain" }

command = {
    SOI ~
    (
    watch
     | comm_where
     | print_state
     | print
     | print_fail
     | delete_watch
     | delete
     | brk
     | enable
     | disable
     | step_over
     | step
     // commands without input
     | cont
     | help
     | info_break
     | info_watch
     | display
     | exit
     | explain
    )?
    ~ EOI
}
